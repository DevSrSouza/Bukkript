plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'br.com.devsrsouza'
version '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    jcenter()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    compileOnly("br.com.devsrsouza.kotlinbukkitapi:core:0.1.0-SNAPSHOT")
    compileOnly("br.com.devsrsouza.kotlinbukkitapi:attributestorage:0.1.0-SNAPSHOT")
    compileOnly("br.com.devsrsouza.kotlinbukkitapi:plugins:0.1.0-SNAPSHOT")
    compileOnly("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    compile("org.jetbrains.kotlin:kotlin-compiler-embeddable")
    compile("org.jetbrains.kotlin:kotlin-script-runtime")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.jetbrains.kotlin:kotlin-scripting-common")

    compileOnly('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT')
}

shadowJar {
    baseName = 'Bukkript'
    classifier = null

    dependencies {
        // kotlin-compiler-embeddable
        include(dependency("org.jetbrains.kotlin:kotlin-compiler-embeddable"))
        include(dependency("org.jetbrains.kotlin:kotlin-reflect"))
        include(dependency("org.jetbrains.kotlin:kotlin-script-runtime"))
    }
}

bukkit {
    author = 'DevSrSouza'

    main = 'br.com.devsrsouza.bukkript.Bukkript'

    depend = ['KotlinBukkitAPI']
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MavenBuild(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'bukkript'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Bukkript is a Bukkit plugin that allows server admins to customize their server easily with the power of Kotlin language and KotlinBukkitAPI.')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/DevSrSouza/Bukkript')
            }
        }
    }
}